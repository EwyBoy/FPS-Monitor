plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.MOD_NAME
version = project.MC_VERSION_GENERAL + "-" + project.VERSION + "-" + project.MOD_LOADER
group = project.MAVEN_GROUP

repositories {
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.terraformersmc.com/" }
    maven { url = "https://maven.gegy.dev" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.MC_VERSION}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${project.LOADER_VERSION}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.FABRIC_VERSION}"

    includeFabricApiModule "fabric-key-binding-api-v1"

    // https://maven.terraformersmc.com/releases/com/terraformersmc/modmenu/
    modImplementation "com.terraformersmc:modmenu:${project.MODMENU_VERSION}"

    // https://maven.shedaniel.me/me/shedaniel/cloth/cloth-config-fabric
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.CLOTH_CONFIG_VERSION}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include("me.shedaniel.cloth:cloth-config-fabric:${project.CLOTH_CONFIG_VERSION}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    /*modRuntime ("supercoder79:databreaker:${project.DATABREAKER_VERSION}") {
        exclude module: "fabric-loader"
    }*/
}

def includeFabricApiModule(String moduleName) {
    Object dependency = fabricApi.module(moduleName, project.FABRIC_VERSION)
    dependencies.modImplementation(dependency)
    dependencies.include(dependency)
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.MOD_NAME}"}
    }
}

curseforge {
    if (project.hasProperty('CF_API_KEY')){
        apiKey = project.getProperty('CF_API_KEY')
    }
    project {
        id = "${PROJECT_ID}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        releaseType = "${RELEASE_TYPE}"
        addGameVersion "${MOD_LOADER}"
        addGameVersion "${JAVA_VERSION}"
        addGameVersion '1.19.2'
        addGameVersion '1.19.1'

        relations {
            optionalDependency 'fabric-api'
        }

        mainArtifact(remapJar) {
            displayName = "${MOD_NAME}-${MC_VERSION_GENERAL}-${VERSION}-${MOD_LOADER}.jar"
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}